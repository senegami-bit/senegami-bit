Una zapatera necesita registrar los datos de sus vendedores y los datos de las ventas individuales para calcular algunos estándares de calidad a fin de mejorar la productividad y otorgar bonos, así como "cortar cabezas". 

El sistema debe solicitar el a cuántos vendedores se va a registrar su información y de cada vendedor debe solicitar cuántas ventas realizó y de cuánto fue el valor de cada una. 

Después de terminar el registro de las ventas, debe mostrar el total de las ventas de ese vendedor, el número de ventas que hizo y su venta más alta. Si la venta más alta es menor a $100 o el total es menor de $1,500, debe mostrar un mensaje de que pase a Recursos Humanos. 

El reporte final debe entregar un reporte de cuántos vendedores se registraron en el sistema y cuánto fue el total de las ventas globales de la empresa . Si el reporte global es mayor a $20,000 deberá mostrar un mensaje de que se alcanzó la meta , de lo contrario mostrará un mensaje que habrá que convocar una reunión . 
Algoritmo llegaronlostopgloblales 
			
			Definir numVendedores, numVentas, i, j Como Entero
			Definir valorVenta, totalVendedor, ventaMaxima, totalGlobal Como Real
			
			Escribir "Ingrese el número de vendedores a registrar:"
			Leer numVendedores
			
			
			totalGlobal <- 0
			
			
			Para i <- 1 Hasta numVendedores Hacer
				Escribir "Ingrese el número de ventas realizadas por el vendedor ", i, ":"
				Leer numVentas
				
				
				totalVendedor <- 0
				ventaMaxima <- 0
				
				
				Para j <- 1 Hasta numVentas Hacer
					Escribir "Ingrese el valor de la venta ", j, " del vendedor ", i, ":"
					Leer valorVenta
					
					totalVendedor <- totalVendedor + valorVenta
					
					Si valorVenta > ventaMaxima Entonces
						ventaMaxima <- valorVenta
					FinSi
				FinPara
				
	
				Escribir "Resumen para el vendedor ", i, ":"
				Escribir "Total de ventas: $", totalVendedor
				Escribir "Número de ventas realizadas: ", numVentas
				Escribir "Venta más alta: $", ventaMaxima
				
				
				Si ventaMaxima < 100 O totalVendedor < 1500 Entonces
					Escribir "El vendedor ", i, " debe pasar a Recursos Humanos."
				FinSi
				
				
				totalGlobal <- totalGlobal + totalVendedor
			FinPara
			
			
			Escribir "Reporte Final:"
			Escribir "Número total de vendedores registrados: ", numVendedores
			Escribir "Total de ventas globales de la empresa: $", totalGlobal
			
			Si totalGlobal > 20000 Entonces
				Escribir "¡Se alcanzó la meta!"
			Sino
				Escribir "Es necesario convocar una reunión."
			FinSi

FinAlgoritmo
*** Ejecución Iniciada. ***
Ingrese el número de vendedores a registrar:
> 1
Ingrese el número de ventas realizadas por el vendedor 1:
> 2
Ingrese el valor de la venta 1 del vendedor 1:
> 600
Ingrese el valor de la venta 2 del vendedor 1:
> 600
Resumen para el vendedor 1:
Total de ventas: $1200
Número de ventas realizadas: 2
Venta más alta: $600
El vendedor 1 debe pasar a Recursos Humanos.
Reporte Final:
Número total de vendedores registrados: 1
Total de ventas globales de la empresa: $1200
Es necesario convocar una reunión.
*** Ejecución Finalizada. ***


